buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.trello:victor:$victor_version"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

// Make sure to apply this plugin *after* the Android plugin
apply plugin: 'com.trello.victor'

// REF: Firmado automáticamente: https://stackoverflow.com/a/43230396/5136913
def keystorePropertiesFile = rootProject.file("../../private/android/keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "com.jaureguialzo.turnoclaseprofesor"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 31
        versionName "2.2.8"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        multiDexEnabled true
    }
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
        }
    }
    sourceSets {
        main {
            svg.srcDir 'src/main/svg'
        }
        main.java.srcDirs += 'src/main/kotlin'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildToolsVersion "$build_tools_version"
}

victor {
    // Any assets defined in relative terms needs a base DPI specified
    svgDpi = 72

    // Do not generate these densities for SVG assets
    excludeDensities = ['ldpi']
}

// REF: Generar árbol de dependencias: https://stackoverflow.com/a/42374426/5136913
dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.media:media:$media_version"
    implementation "androidx.legacy:legacy-support-v4:$legacy_support_version"
    implementation "androidx.multidex:multidex:$multidex_support_lib_version"
    implementation "com.google.firebase:firebase-core:$firebase_core_version"
    implementation "com.google.firebase:firebase-auth:$firebase_auth_version"
    implementation "com.google.firebase:firebase-firestore:$firebase_firestore_version"
    implementation 'com.google.firebase:firebase-functions-ktx:19.0.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // REF: Dependencias para tests: https://stackoverflow.com/a/56624085
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test:core:1.2.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation "tools.fastlane:screengrab:$fastlane_screengrab_version"
}

apply plugin: 'com.google.gms.google-services'
repositories {
    mavenCentral()
}
